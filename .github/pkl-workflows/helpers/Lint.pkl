module lint

import "../GithubAction/GithubAction.pkl" as gha
import "Steps.pkl"

function lint(): gha.Job = new {
  name = "Verify TODOs"
  `runs-on` = new gha.UbuntuLatest{}
  steps {
    Steps.checkout()
    new gha.Step {
      uses = "nirinchev/verify-todo@9be6f76daddad71433e5deb1b58c517490e5c66e"
      with {
        ["token"] = "${{ secrets.GITHUB_TOKEN }}"
        ["include"] = "**/*.+(cs|cpp|hpp)"
        ["exclude"] = "wrappers/realm-core/**"
        ["pattern"] = "\\\\WR[A-Z]+-[0-9]+"
      }
    }
  }
}

function verifyNamespaces(packageJob: String): gha.Job = new {
  `runs-on` = new gha.UbuntuLatest{}
  name = "Verify Namespaces"
  needs {
    packageJob
  }
  steps {
    Steps.checkout()
    ...Steps.fetchPackages(packageJob, null)
    new {
      run = "dotnet tool install ilspycmd -g --version 8.0.0.7345"
    }
    new {
      name = "Verify Namespaces"
      run = """
        $isFailure = $false
        Get-ChildItem ./Realm/packages -Filter *.nupkg | Foreach-Object {
          $targetPath = Join-Path ./Realm/packages $_.BaseName
          Expand-Archive $_.FullName -DestinationPath $targetPath

          Get-ChildItem $targetPath -Filter *.dll -Recurse | ForEach-Object {
            if (-not ($_.FullName -match "runtimes")) {
              $ilspyOutput = ilspycmd $_.FullName

              $parentDll = $_.FullName

              $ilspyOutput | ForEach-Object {
                if ($_ -match "namespace.*Realm(\\.|$)") {
                  Write-Output "::error file=$parentDll::Realm present in namespace - $($_)"
                  Set-Variable "isFailure" -Value $true
                }
              }
            }
          }
        }

        if ($isFailure) {
          exit 1
        }
        """
      shell = "pwsh"
    }
  }
}