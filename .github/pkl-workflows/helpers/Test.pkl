module test

import "../GithubAction/GithubAction.pkl" as gha
import "Common.pkl"
import "Steps.pkl"
import "BaaS.pkl"

local const outputFile = "TestResults.xml"

// Public test targets
function netFramework(_packageJob: String, _syncDifferentiators: Listing<String>): gha.StepJobBase = testJob(
  new TestConfig {
    title = ".NET Framework"
    packageJob = _packageJob
    syncDifferentiator = "net-framework"
    syncDifferentiators = _syncDifferentiators
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(_packageJob, "Tests/Realm.Tests", new Mapping {
      ["TargetFramework"] = "net461"
    })
    new gha.Step {
      name = "Run the tests"
      run = "./Tests/Realm.Tests/bin/\(Common.configuration)/net461/Realm.Tests.exe --result=\(outputFile) --labels=After \(baasTestArgs(config))"
    }
    ...reportTestResults(config)
  })

function netCore(_packageJob: String, frameworks: Listing<Common.NetFramework>): gha.StepJobBase = testJob(
  new TestConfig {
    packageJob = _packageJob
    title = "${{ matrix.framework }}, ${{ (matrix.os.runner == 'win81' && 'win81') || matrix.os.runtime }}"
    shouldCleanWorkspace = true
  },
  "${{ matrix.os.runner }}",
  new gha.MatrixStrategy {
    matrix {
      ["framework"] = frameworks
      ["os"] = new Listing<MatrixOS> {
        new { runner = "windows-latest" runtime = "win-x64" }
        new { runner = "ubuntu-latest" runtime = "linux-x64" }
        new { runner = "macos-13" runtime = "osx-x64" }
      }
      ["include"] = new Listing {
        new Mapping {
          ["framework"] = "net8.0"
          ["os"] = new MatrixOS { runner = "macos-14" runtime = "osx-arm64" }
        }
      }
    }
  },
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    // To avoid NU5037 error
    new gha.Step {
      name = "Clear nuget cache"
      run = "dotnet nuget locals all --clear"
      `if` = "${{ matrix.os.runner == 'win81' }}"
    }
    new gha.Step {
      id = "get-net-version"
      name = "Extract .NET version"
      run = """
          NET_VERSION=$(echo '${{ matrix.framework }}.x' | sed 's/net//g')
          echo "version=$NET_VERSION" >> $GITHUB_OUTPUT
        """
      shell = "bash"
    }
    Steps.setupDotnet("${{ steps.get-net-version.outputs.version }}")
    ...Steps.dotnetPublish("Tests/Realm.Tests", "${{ matrix.framework }}", "${{ matrix.os.runtime }}", getTestProps(_packageJob))
    ...dotnetRunTests(true)
    ...reportTestResults(config)
  })

function weaver(): gha.StepJobBase = testJob(
  new TestConfig {
    title = "Weaver"
    shouldCleanWorkspace = true
  },
  "${{ matrix.os.runner }}",
  new gha.MatrixStrategy {
    matrix {
      ["os"] = new Listing<MatrixOS> {
        new { runner = "windows-latest" runtime = "win-x64" }
        new { runner = "ubuntu-latest" runtime = "linux-x64" }
        new { runner = "macos-14" runtime = "osx-arm64" }
      }
    }
  },
  (config) -> new Listing<gha.Step> {
    ...Steps.checkout()
    ...Steps.setupWorkloads("android ${{ (matrix.os.runner != 'ubuntu-latest' && 'tvos ios maccatalyst') || '' }}", "6.0.x").toList()
    ...Steps.dotnetPublish("Tests/Weaver/Realm.Fody.Tests", "net6.0", "${{ matrix.os.runtime }}", new Mapping{}).toList()
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  }
)

function sourceGeneration(): gha.StepJobBase = testJob(
  new TestConfig {
    title = "Source Generation"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...Steps.checkout()
    ...Steps.setupWorkloads("tvos ios maccatalyst android", "6.0.x").toList()
    ...Steps.dotnetPublish("Tests/SourceGenerators/Realm.SourceGenerator.Tests", "net6.0", "win-x64", new Mapping{}).toList()
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  })

function wovenClasses(_packageJob: String): gha.StepJobBase = testJob(
  new TestConfig {
    packageJob = _packageJob
    title = "Woven Classes"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...Steps.dotnetPublish("Tests/Realm.Tests", "net8.0", "win-x64", (getTestProps(_packageJob)) {
      ["TestWeavedClasses"] = "true"
    })
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  }
)

function tvOS(_packageJob: String, _syncDifferentiators: Listing<String>): gha.StepJobBase = testJob(
  new TestConfig {
    packageJob = _packageJob
    title = "Xamarin.tvOS"
    syncDifferentiator = "tvos"
    syncDifferentiators = _syncDifferentiators
  },
  "macos-12",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(_packageJob, "Tests/Tests.XamarinTVOS", new Mapping {
      ["Platform"] = "iPhoneSimulator"
    })
    Steps.runSimulator(new Steps.SimulatorConfig{
      appPath = "Tests/Tests.XamarinTVOS/bin/iPhoneSimulator/\(Common.configuration)/Tests.XamarinTVOS.app"
      arguments = "--headless --result=${{ github.workspace }}/\(outputFile) --labels=All \(baasTestArgs(config))"
      bundleId = "io.realm.Tests-XamarinTVOS"
      iphoneToSimulate = "Apple-TV-1080p"
      os = "tvOS"
    })
    ...reportTestResults(config)
  }
)

// Private helpers
local function testJob(config: TestConfig, runsOn: gha.Machine | String, _strategy: gha.MatrixStrategy?, _steps: Function1<TestConfig, Listing<gha.Step>>): gha.StepJobBase = (if (_strategy != null) new gha.MatrixJob{
  strategy = _strategy
} else new gha.Job{}) {
  name = "Test \(config.title)"
  `runs-on` = runsOn
  when (config.packageJob != null) {
    needs {
      config.packageJob!!
    }
  }
  `if` = Common.ifNotCanceledCondition
  `timeout-minutes` = Common.testTimeout
  steps = _steps.apply(config)
}

local function prepareTests(config: TestConfig): Listing<gha.Step> = new Listing {
  ...Steps.checkout()
  ...cleanWorkspace(config.shouldCleanWorkspace)
  ...fetchTestArtifacts(config.packageJob!!, config.useWrappers)
  ...BaaS.deployStep(config.syncDifferentiator, config.runSyncTests)
}

local function cleanWorkspace(shouldClean: Boolean): Listing<gha.Step> = new Listing {
  when (shouldClean) {
    new gha.Step {
      name = "Cleanup Workspace"
      run = "git clean -fdx"
    }
  }
}

local function fetchTestArtifacts(packageJob: String, useWrappers: Boolean): List<gha.Step> = if (useWrappers)
  Steps.fetchPackages(packageJob, Common.nugetPackages)
  else Steps.fetchPackages(packageJob, Common.nugetPackages)

local function buildTests(packageJob: String, projectPath: String, properties: Mapping<String, String>): List<gha.Step> = Steps.msbuild(projectPath, new Mapping{
  ...getTestProps(packageJob)
  ...properties
})

local function baasTestArgs(config: TestConfig): String = if (config.runSyncTests) " --baasaas-api-key=${{ secrets.BAASAAS_API_KEY}} --baas-differentiator=\(config.syncDifferentiator)-${{ github.run_id }}-${{ github.run_attempt }}" else ""

local function reportTestResults(config: TestConfig): Listing<gha.Step> = new {
  local _outputFile = if (config.transformResults) "\(outputFile)_transformed.xml" else outputFile
  when (config.transformResults) {
    new gha.Step {
      name = "Transform Results"
      run = "xsltproc --output \(_outputFile) Tests/Realm.Tests/EmbeddedResources/nunit3-junit.xslt \(outputFile)"
    }
  }
  new gha.Step {
    name = "Publish Unit Test Results"
    uses = "LaPeste/test-reporter@510caf50a955b1003bec48a6494be4d6537f3a0b"
    `if` = "always()"
    with {
      ["name"] = "Results \(config.title)"
      ["path"] = _outputFile
      ["reporter"] = "java-junit"
      ["list-suites"] = "failed"
      ["path-replace-backslashes"] = true
      ["fail-on-error"] = true
    }
  }
}

local function getTestProps(packageJob: String): Mapping<String, String> = new Mapping {
  ["RestoreConfigFile"] = "Tests/Test.NuGet.Config"
  ["UseRealmNupkgsWithVersion"] = "${{ needs.\(packageJob).outputs.package_version }}"
  ["RealmTestsStandaloneExe"] = "true"
}

local function dotnetRunTests(enableCoreDumps: Boolean): Listing<gha.Step> = new Listing {
  new gha.Step {
    name = "Run the tests"
    run = "${{ steps.dotnet-publish.outputs.executable-path }} --result=\(outputFile) --labels=After"
    when (enableCoreDumps) {
      env {
        ["DOTNET_DbgEnableMiniDump"] = 1
        ["DOTNET_EnableCrashReport"] = 1
      }
    }
  }
  when (enableCoreDumps) {
    new gha.Step {
      name = "Archive core dump"
      uses = "actions/upload-artifact@v4"
      `if` = "${{ failure() && runner.os != 'Windows' }}"
      with {
        ["name"] = "crash-report-net-core-${{ runner.os }}-${{ runner.arch }}"
        ["path"] = "/tmp/coredump*"
      }
    }
  }
}

local class MatrixOS {
  runner: "windows-latest" | "ubuntu-latest" | "macos-13" | "macos-14"
  runtime: Common.NetRuntime
}

local class TestConfig {
  title: String
  packageJob: String? = null
  useWrappers: Boolean = false
  shouldCleanWorkspace: Boolean = false
  syncDifferentiator: String? = null
  transformResults: Boolean = false
  syncDifferentiators: Listing<String>?
  runSyncTests: Boolean = syncDifferentiators?.toList()?.contains(syncDifferentiator) ?? false
}
