module test

import "../GithubAction/GithubAction.pkl" as gha
import "Common.pkl"
import "Steps.pkl"
import "BaaS.pkl"
import "Package.pkl"

local const actionReportTestResults = "dorny/test-reporter@31a54ee7ebcacc03a09ea97a7e5465a47b84aea5"

local const outputFile = "TestResults.xml"

// Public test targets
function netFramework(_syncDifferentiators: Listing<String>): gha.StepJobBase = testJob(
  new TestConfig {
    title = ".NET Framework"
    needsPackages = true
    syncDifferentiator = "net-framework"
    syncDifferentiators = _syncDifferentiators
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Realm.Tests"
      }
      properties {
        ["TargetFramework"] = "net461"
      }
      standaloneExe = true
    })
    new gha.Step {
      name = "Run the tests"
      run = "./Tests/Realm.Tests/bin/\(Common.configuration)/net461/Realm.Tests.exe --result=\(outputFile) --labels=After \(baasTestArgs(config))"
    }
    ...reportTestResults(config)
  })

function netCore(frameworks: Listing<Common.NetFramework>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "${{ matrix.framework }}, ${{ (matrix.os.runner == 'win81' && 'win81') || matrix.os.runtime }}"
    shouldCleanWorkspace = true
  },
  "${{ matrix.os.runner }}",
  new gha.MatrixStrategy {
    matrix {
      ["framework"] = frameworks
      ["os"] = new Listing<MatrixOS> {
        new { runner = "windows-latest" runtime = "win-x64" }
        new { runner = "ubuntu-latest" runtime = "linux-x64" }
        new { runner = "macos-13" runtime = "osx-x64" }
      }
      ["include"] = new Listing {
        new Mapping {
          ["framework"] = "net8.0"
          ["os"] = new MatrixOS { runner = "macos-14" runtime = "osx-arm64" }
        }
      }
    }
  },
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    // To avoid NU5037 error
    new gha.Step {
      name = "Clear nuget cache"
      run = "dotnet nuget locals all --clear"
      `if` = "${{ matrix.os.runner == 'win81' }}"
    }
    new gha.Step {
      id = "get-net-version"
      name = "Extract .NET version"
      run = """
          NET_VERSION=$(echo '${{ matrix.framework }}.x' | sed 's/net//g')
          echo "version=$NET_VERSION" >> $GITHUB_OUTPUT
        """
      shell = "bash"
    }
    Steps.setupDotnet("${{ steps.get-net-version.outputs.version }}")
    ...Steps.dotnetPublish("Tests/Realm.Tests", "${{ matrix.framework }}", "${{ matrix.os.runtime }}", getTestProps(true))
    ...dotnetRunTests(true)
    ...reportTestResults(config)
  })

function weaver(): gha.StepJobBase = testJob(
  new TestConfig {
    title = "Weaver"
    shouldCleanWorkspace = true
  },
  "${{ matrix.os.runner }}",
  new gha.MatrixStrategy {
    matrix {
      ["os"] = new Listing<MatrixOS> {
        new { runner = "windows-latest" runtime = "win-x64" }
        new { runner = "ubuntu-latest" runtime = "linux-x64" }
        new { runner = "macos-14" runtime = "osx-arm64" }
      }
    }
  },
  (config) -> new Listing<gha.Step> {
    ...Steps.checkout(false)
    ...Steps.setupWorkloads(null, "6.0.x")
    ...Steps.dotnetPublish("Tests/Weaver/Realm.Fody.Tests", "net6.0", "${{ matrix.os.runtime }}", new Mapping{}).toList()
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  }
)

function sourceGeneration(): gha.StepJobBase = testJob(
  new TestConfig {
    title = "Source Generation"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...Steps.checkout(false)
    ...Steps.setupWorkloads(null, "6.0.x")
    ...Steps.dotnetPublish("Tests/SourceGenerators/Realm.SourceGenerator.Tests", "net6.0", "win-x64", new Mapping{}).toList()
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  })

function wovenClasses(): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Woven Classes"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...Steps.dotnetPublish("Tests/Realm.Tests", "net8.0", "win-x64", (getTestProps(true)) {
      ["TestWeavedClasses"] = "true"
    })
    ...dotnetRunTests(false)
    ...reportTestResults(config)
  }
)

function tvOS(_syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Xamarin.tvOS"
    syncDifferentiator = "tvos"
    syncDifferentiators = _syncDifferentiators
  },
  "macos-12",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Tests.XamarinTVOS"
      }
      properties {
        ["Platform"] = "iPhoneSimulator"
      }
    })
    Steps.runSimulator(new Steps.SimulatorConfig{
      appPath = "Tests/Tests.XamarinTVOS/bin/iPhoneSimulator/\(Common.configuration)/Tests.XamarinTVOS.app"
      arguments = "--headless --result=${{ github.workspace }}/\(outputFile) --labels=All \(baasTestArgs(config))"
      bundleId = "io.realm.Tests-XamarinTVOS"
      iphoneToSimulate = "Apple-TV-1080p"
      os = "tvOS"
    })
    ...reportTestResults(config)
  })

function iOS_Xamarin(): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Xamarin.iOS"
  },
  "macos-12",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Tests.iOS"
      }
      properties {
        ["Platform"] = "iPhoneSimulator"
      }
    })
    Steps.runSimulator(new Steps.SimulatorConfig{
      appPath = "Tests/Tests.iOS/bin/iPhoneSimulator/\(Common.configuration)/Tests.iOS.app"
      arguments = "--headless --result=${{ github.workspace }}/\(outputFile) --labels=All \(baasTestArgs(config))"
      bundleId = "io.realm.dotnettests"
      iphoneToSimulate = "iPhone-8"
      os = "iOS"
    })
    ...reportTestResults(config)
  })

function iOS_Maui(_syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Maui.iOS"
    syncDifferentiator = "ios-maui"
    syncDifferentiators = _syncDifferentiators
    transformResults = true
  },
  "macos-13",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...Steps.setupWorkloads("maui", null)
    Steps.setupXcode("latest-stable")
    Steps.dotnetBuild("Tests/Tests.Maui", "net8.0-ios", null, getTestProps(false))
    Steps.runSimulator(new Steps.SimulatorConfig{
      appPath = "Tests/Tests.Maui/bin/\(Common.configuration)/net8.0-ios/iossimulator-x64/Tests.Maui.app"
      arguments = "--headless --result=${{ github.workspace }}/\(outputFile) --labels=All \(baasTestArgs(config))"
      bundleId = "io.realm.mauitests"
      iphoneToSimulate = "iPhone-15"
      os = "iOS"
    })
    ...reportTestResults(config)
  })

function macOS_Xamarin(): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Xamarin.macOS"
  },
  "macos-12",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Tests.XamarinMac"
      }
    })
    new {
      name = "Run the tests"
      run = "Tests/Tests.XamarinMac/bin/\(Common.configuration)/Tests.XamarinMac.app/Contents/MacOS/Tests.XamarinMac --headless --result=${{ github.workspace }}/\(outputFile) --labels=All"
    }
    ...reportTestResults(config)
  })

function macOS_Maui(_syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Maui.MacCatalyst"
    syncDifferentiator = "macos-maui"
    syncDifferentiators = _syncDifferentiators
    transformResults = true
  },
  "macos-13",
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    ...Steps.setupWorkloads("maui", null)
    Steps.setupXcode("latest-stable")
    Steps.dotnetBuild("Tests/Tests.Maui", "net8.0-maccatalyst", null, getTestProps(false))
    new {
      name = "Run the tests"
      run = "Tests/Tests.Maui/bin/\(Common.configuration)/net8.0-maccatalyst/maccatalyst-x64/Tests.Maui.app/Contents/MacOS/Tests.Maui --headless --result=${{ github.workspace }}/\(outputFile) --labels=All \(baasTestArgs(config))"
    }
    ...reportTestResults(config)
  })

function uwp(_syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "UWP"
    syncDifferentiators = _syncDifferentiators
    syncDifferentiator = "uwp"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    new {
      name = "Import test certificate"
      run = """
        $pfx_cert_byte = [System.Convert]::FromBase64String("${{ secrets.Base64_Encoded_Pfx }}")
        $currentDirectory = Get-Location
        [IO.File]::WriteAllBytes("${{ github.workspace }}\\Tests\\Tests.UWP\\Tests.UWP_TemporaryKey.pfx", $pfx_cert_byte)
        certutil -f -p "${{ secrets.Pfx_Password }}" -importpfx my ${{ github.workspace }}\\Tests\\Tests.UWP\\Tests.UWP_TemporaryKey.pfx
        """
      shell = "powershell"
    }
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Tests.UWP"
      }
      properties {
        ["AppxBundle"] = "Always"
        ["PackageCertificateKeyFile"] = "${{ github.workspace }}\\Tests\\Tests.UWP\\Tests.UWP_TemporaryKey.pfx"
        ["PackageCertificatePassword"] = "${{ secrets.Pfx_Password }}"
        ["UseDotNetNativeToolchain"] = "false"
        ["AppxBundlePlatforms"] = "x64"
      }
    })
    new {
      name = "Run the tests"
      run = "./Tests/Tests.UWP/RunTests.ps1 -ExtraAppArgs '\(baasTestArgs(config))'"
      shell = "powershell"
    }
    ...reportTestResultsWithCustomFile("${{ env.TEST_RESULTS }}", config)
  })

function android_Xamarin(): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Xamarin.Android"
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    Steps.setupJDK()
    ...prepareTests(config)
    ...buildTests(new Steps.MSBuildConfig {
      projects {
        "Tests/Tests.Android"
      }
      target = "SignAndroidPackage"
      properties {
        ["AndroidUseSharedRuntime"] = "False"
        ["EmbedAssembliesIntoApk"] = "True"
      }
    })
    ...Steps.runDeviceFarm(new Steps.DeviceFarmConfig {
      apkPath = "${{ github.workspace }}/Tests/Tests.Android/bin/Release/io.realm.xamarintests-Signed.apk"
      appId = "io.realm.xamarintests"
    })
    ...reportTestResultsWithCustomFile("${{ steps.run_tests.outputs.test-results-path }}", config)
  })

function android_Maui(_syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = testJob(
  new TestConfig {
    needsPackages = true
    title = "Maui.Android"
    syncDifferentiator = "android-maui"
    syncDifferentiators = _syncDifferentiators
    transformResults = true
  },
  new gha.WindowsLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    Steps.setupJDK()
    ...prepareTests(config)
    ...Steps.setupWorkloads("maui", null)
    ...Steps.dotnetPublish("Tests/Tests.Maui", "net8.0-android", /* runtime */ null, getTestProps(false))
    ...Steps.runDeviceFarm(new Steps.DeviceFarmConfig {
      apkPath = "${{ github.workspace }}/Tests/Tests.Maui/bin/Release/net8.0-android/publish/io.realm.mauitests-Signed.apk"
      appId = "io.realm.mauitests"
    })
    ...reportTestResultsWithCustomFile("${{ steps.run_tests.outputs.test-results-path }}", config)
  })

function codeCoverage(wrappersJob: String, _syncDifferentiators: Listing<Common.SyncDifferentiator>): gha.StepJobBase = (testJob(
  new TestConfig {
    title = "Code Coverage"
    syncDifferentiator = "code-coverage"
    syncDifferentiators = _syncDifferentiators
    usedWrappers = List("linux-x86_64")
  },
  new gha.UbuntuLatest{},
  null,
  (config) -> new Listing<gha.Step> {
    ...prepareTests(config)
    new gha.Step {
      name = "Setup Coverlet & Report Generator"
      run = """
        dotnet tool install coverlet.console --tool-path tools
        dotnet tool install dotnet-reportgenerator-globaltool --tool-path tools
        echo "${{ github.workspace }}/tools" >> $GITHUB_PATH
        """
    }
    ...Steps.dotnetPublish("Tests/Realm.Tests", "net8.0", "linux-x64", new Mapping { ["RealmTestsStandaloneExe"] = "true" })
    new gha.Step {
      name = "Run the tests"
      run = "./tools/coverlet ./Tests/Realm.Tests/bin/\(Common.configuration)/net8.0/linux-x64 -t ${{ steps.dotnet-publish.outputs.executable-path }} -a '--result=\(outputFile) --labels=After\(baasTestArgs(config))' -f lcov -o ./report.lcov --exclude '[Realm.Tests]*' --exclude '[Realm.Fody]*' --exclude '[Realm.PlatformHelpers]*'"
    } |> enableCoreDumps(true)
    archiveCoreDump()
    ...Steps.publishCoverage("./report.lcov")
    ...reportTestResults(config)
  })) {
    needs {
      wrappersJob
    }
  }

function unity(config: UnityTestConfig): Mapping<String, gha.Job> = new Mapping<String, gha.Job> {
  ["build-unity-tests-\(config.os)"] = new gha.Job {
    name = "Build Unity \(config.os)"
    `runs-on` = new Listing<String> {
      "unity"
      config.os
    }
    needs {
      Common.job_Packages
    }
    `timeout-minutes` = 30
    `if` = Common.ifNotCanceledCondition
    steps {
      ...Steps.checkout(false)
      Steps.cleanupWorkspace()
      Steps.downloadArtifacts(Package.unityPkgName, "Realm/Realm.Unity")
      Steps.downloadArtifacts("UnityTests", "Tests/Tests.Unity")
      new gha.Step {
        name = "Build Unity Tests"
        run = "unity-editor -runTests -batchmode -projectPath ${{ github.workspace }}/Tests/Tests.Unity -testPlatform Standalone\(config.platform()) -testSettingsFile ${{ github.workspace }}/Tests/Tests.Unity/.TestConfigs/\(config.settings).json -logFile \(if (config.os == "windows") "build.log" else "-")"
      }
      Steps.uploadArtifacts("UnityTestsRunner.\(config.os)", "Tests/Tests.Unity/Player_Standalone\(config.platform())_\(config.settings)/")
    }
  }
  ["run-unity-tests-\(config.os)"] = new gha.Job {
    name = "Test Unity \(config.os)"
    `runs-on` = config.runsOn()
    `timeout-minutes` = 30
    `if` = Common.ifNotCanceledCondition
    needs {
      "build-unity-tests-\(config.os)"
    }
    steps {
      ...Steps.checkout(false)
      Steps.downloadArtifacts("UnityTestsRunner.\(config.os)", "TestRunner")
      when (config.os == "linux") {
        new gha.Step {
          name = "Install xvfb"
          run = "sudo apt install -y xvfb libglu1 libxcursor1"
        }
        new gha.Step {
          name = "Run Tests"
          run = """
            chmod +x ${{ github.workspace }}/TestRunner/PlayerWithTests.x86_64
            xvfb-run --auto-servernum --server-args='-screen 0 640x480x24:32' ${{ github.workspace }}/TestRunner/PlayerWithTests.x86_64 -logFile - --result=${{ github.workspace }}/\(outputFile)
            """
        }
      }
      when (config.os == "windows") {
        new gha.Step {
          name = "Run Tests"
          run = #"""
            Start-Process ${{ github.workspace }}\TestRunner\PlayerWithTests.exe -Wait -ArgumentList "-logFile","${{ github.workspace }}\test.log","--result=${{ github.workspace }}\\#(outputFile)"
            cat ${{ github.workspace }}\test.log
            """#
          shell = "pwsh"
        }
      }
      ...reportTestResults(new TestConfig {
        title = "Unity \(config.os) \(config.settings)"
      })
    }
  }
}

class UnityTestConfig {
  os: "windows" | "linux"
  settings: String = "Mono-Net4"

  function platform() = "\(os.capitalize())64"
  function runsOn():gha.Machine = if (os == "windows") new gha.WindowsLatest{} else if (os == "linux") new gha.UbuntuLatest{} else throw("invalid os: \(os)")
}

// Private helpers
local function testJob(config: TestConfig, runsOn: gha.Machine | String, _strategy: gha.MatrixStrategy?, _steps: Function1<TestConfig, Listing<gha.Step>>): gha.StepJobBase = (if (_strategy != null) new gha.MatrixJob{
  strategy = _strategy
} else new gha.Job{}) {
  name = "Test \(config.title)"
  `runs-on` = runsOn
  when (config.needsPackages) {
    needs {
      Common.job_Packages
      when (config.runSyncTests) {
        Common.job_Baas
      }
    }
  }
  `if` = Common.ifNotCanceledCondition
  `timeout-minutes` = Common.testTimeout
  steps = _steps.apply(config)
}

local function prepareTests(config: TestConfig(needsPackages == true || !usedWrappers.isEmpty)): Listing<gha.Step> = new Listing {
  ...Steps.checkout(false)
  ...cleanWorkspace(config.shouldCleanWorkspace)
  ...fetchTestArtifacts(config.usedWrappers)
  ...BaaS.deployStep(config.syncDifferentiator, config.runSyncTests)
}

local function cleanWorkspace(shouldClean: Boolean): Listing<gha.Step> = new Listing {
  when (shouldClean) {
    Steps.cleanupWorkspace()
  }
}

local function fetchTestArtifacts(wrappers: List<String>(every((wrapper) -> Common.wrapperBinaryNames.contains(wrapper)))): List<gha.Step> =
  if (!wrappers.isEmpty) Steps.fetchWrappers(wrappers)
  else Steps.fetchPackages(Common.nugetPackages)

local function buildTests(config: Steps.MSBuildConfig): Listing<gha.Step> = Steps.msbuild((config) {
  properties {
    ...getTestProps(config.standaloneExe)
  }
})

local function baasTestArgs(config: TestConfig): String = if (config.runSyncTests) " --baasaas-api-key=${{ secrets.BAASAAS_API_KEY}} --baas-differentiator=\(config.syncDifferentiator)-${{ github.run_id }}-${{ github.run_attempt }}" else ""

local function reportTestResults(config: TestConfig): Listing<gha.Step> = reportTestResultsWithCustomFile(outputFile, config)

local function reportTestResultsWithCustomFile(_outputFile: String, config: TestConfig): Listing<gha.Step> = new {
  local __outputFile = if (config.transformResults) "\(_outputFile)_transformed.xml" else _outputFile
  when (config.transformResults) {
    new gha.Step {
      name = "Transform Results"
      run = "xsltproc --output \(__outputFile) Tests/Realm.Tests/EmbeddedResources/nunit3-junit.xslt \(_outputFile)"
    }
  }
  new gha.Step {
    name = "Publish Unit Test Results"
    uses = actionReportTestResults
    `if` = "always()"
    with {
      ["name"] = "Results \(config.title)"
      ["path"] = __outputFile
      ["reporter"] = "java-junit"
      ["list-suites"] = "failed"
      ["path-replace-backslashes"] = true
      ["fail-on-error"] = true
    }
  }
}

local function getTestProps(standaloneExe: Boolean): Mapping<String, String> = new Mapping {
  ["RestoreConfigFile"] = "Tests/Test.NuGet.Config"
  ["UseRealmNupkgsWithVersion"] = "${{ needs.\(Common.job_Packages).outputs.\(Package.versionOutput) }}"
  when (standaloneExe) {
    ["RealmTestsStandaloneExe"] = "true"
  }
}

local function dotnetRunTests(enableCoreDumps: Boolean): Listing<gha.Step> = new Listing {
  new gha.Step {
    name = "Run the tests"
    run = "${{ steps.dotnet-publish.outputs.executable-path }} --result=\(outputFile) --labels=After"
  } |> enableCoreDumps(enableCoreDumps)
  when (enableCoreDumps) {
    archiveCoreDump()
  }
}

local function enableCoreDumps(enable: Boolean): Mixin<gha.Step> = new Mixin<gha.Step> {
  when (enable) {
    env {
      ["DOTNET_DbgEnableMiniDump"] = 1
      ["DOTNET_EnableCrashReport"] = 1
    }
  }
}

local function archiveCoreDump(): gha.Step = (Steps.uploadArtifacts("crash-report-net-core-${{ runner.os }}-${{ runner.arch }}", "/tmp/coredump*")) {
  name = "Archive core dump"
  `if` = "${{ failure() && runner.os != 'Windows' }}"
  with {
    ["retention-days"] = 30
    ["if-no-files-found"] = "warn"
  }
}

local class MatrixOS {
  runner: "windows-latest" | "ubuntu-latest" | "macos-13" | "macos-14"
  runtime: Common.NetRuntime
}

local class TestConfig {
  title: String
  needsPackages: Boolean = false
  usedWrappers: List<String>(every((wrapper) -> Common.wrapperBinaryNames.contains(wrapper)))
  shouldCleanWorkspace: Boolean = false
  syncDifferentiator: Common.SyncDifferentiator? = null
  transformResults: Boolean = false
  syncDifferentiators: Listing<Common.SyncDifferentiator>?
  runSyncTests: Boolean = syncDifferentiator != null && (syncDifferentiators?.toList()?.contains(syncDifferentiator) ?? false)
}
